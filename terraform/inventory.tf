resource "local_file" "ansible_inventory" {
  filename = "${path.module}/ansible/k3s/inventory.ini"
  content  = local.ansible_inventory_content
}

locals {
  ansible_inventory_content = join("\n", concat(
    ["# Ansible inventory file - Generated by Terraform"],
    [""],
    flatten([
      for role, instances in {
        "master" = [for name, vm in module.proxmox_vm : { name = name, ip = split("/", split("=", vm.ip_address)[1])[0] } if can(regex("^master", name))],
        "worker" = [for name, vm in module.proxmox_vm : { name = name, ip = split("/", split("=", vm.ip_address)[1])[0] } if can(regex("^worker", name))],
        "nodes"  = [for name, vm in module.proxmox_vm : { name = name, ip = split("/", split("=", vm.ip_address)[1])[0] } if !can(regex("^(master|worker)", name))]
        } : concat(
        ["[${role}]"],
        [for instance in instances : "${instance.name} ansible_host=${instance.ip} ansible_user=${var.ssh_user}"],
        [""]
      ) if length(instances) > 0
    ])
  ))
}